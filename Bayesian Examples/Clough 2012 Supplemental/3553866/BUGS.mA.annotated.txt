model {

	# Specification of the model A


	# Tree-level models

	for (i in 1:n.tree){

		# Number of pods harvested   	

		Npodh[i]~dpois(Npodh.hat.bound[i])
		Npodh.hat.bound[i]<-max(Npodh.hat[i],0.00001) # constraining Npodh.hat to be positive to avoid rounding issues
		Npodh.hat[i]<-exp(a.Npodh.subplot[subplot[i]]+eps.Npodh[i])
		eps.Npodh[i]~dnorm(0,tau.eps.Npodh) # overdispersion

		Npodh.fr[i]<-exp(a.Npodh.subplot.fr[subplot[i]]) # Storing the expected value for i

		# Presence/absence of the ant Philidris cordata
   	
		Pc[i] ~  dbin(p.bound.Pc[i],1)
		p.bound.Pc[i]<- max(0, min(1, p.Pc[i]))
		logit(p.Pc[i])<- a.Pc.subplot[subplot[i]]

		logit(p.Pc.fr[i])<- a.Pc.subplot.fr[subplot[i]] # Storing the expected value for i


		# Number of pods damaged by the mirid Helopeltis sulawesi

		Hs[i] ~ dbin(p.bound.Hs1[i],new.Npodh[i])
		# Next 3 lines contain code to ensure model runs when Npodh=0 
   		new.Npodh[i]<-Npodh[i]+equals(0,Npodh[i])  		
		p.bound.Hs1[i]<-p.bound.Hs2[i]*(1-equals(0, Npodh[i]))
		p.bound.Hs2[i]<- max(0, min(1, p.Hs[i]))		
		logit(p.Hs[i])<- a.Hs.subplot[subplot[i]]+b.Pc_Hs*Pc[i]+eps.Hs[i]
		eps.Hs[i]~dnorm(0,tau.eps.Hs) # overdispersion

		logit(p.Hs.fr[i])<- a.Hs.subplot.fr[subplot[i]]+b.Pc_Hs*Pc[i] # Storing the expected value for i
		
		# Number of pods damaged by the borer Conopomorpha cramerella

		Cc[i] ~ dbin(p.bound.Cc1[i],new.Npodh[i])
		# Next 3 lines contain code to ensure model runs when Npodh=0 
		p.bound.Cc1[i]<-p.bound.Cc2[i]*(1-equals(0, Npodh[i]))
		p.bound.Cc2[i]<- max(0, min(1, p.Cc[i]))		
		logit(p.Cc[i])<- a.Cc.subplot[subplot[i]]+b.Pc_Cc*Pc[i]+b.Hs_Cc*Hs[i]/new.Npodh[i]+eps.Cc[i]
		eps.Cc[i]~dnorm(0,tau.eps.Cc) # overdispersion

		logit(p.Cc.fr[i])<- a.Cc.subplot.fr[subplot[i]]+b.Pc_Cc*Pc[i]+b.Hs_Cc*Hs[i]/new.Npodh[i] # Storing the expected value for i
				
	}


	# Subplot-level models

	for (j in 1:n.subplot){

		# Number of pods harvested   	
		
		a.Npodh.subplot[j]~dnorm(a.Npodh.subplot.hat[j],tau.Npodh.subplot)           
		a.Npodh.subplot.hat[j]<-a.Npodh.plot[plot[j]]+b.Nfert_Npodh*Nfert[j]
		a.Npodh.subplot.fr[j]<-a.Npodh.plot.hat[plot[j]]+b.Nfert_Npodh*Nfert[j] # Storing the expected value for j
		
		# Presence/absence of the ant Philidris cordata

		
		a.Pc.subplot[j]<-a.Pc.plot[plot[j]]+b.Nfert_Pc*Nfert[j]
		a.Pc.subplot.fr[j]<-a.Pc.plot.hat[plot[j]]+b.Nfert_Pc*Nfert[j] # Storing the expected value for j

		# Number of pods damaged by the mirid Helopeltis sulawesi

		a.Hs.subplot[j]~dnorm(a.Hs.subplot.hat[j],tau.Hs.subplot)
		a.Hs.subplot.hat[j]<-a.Hs.plot[plot[j]]+b.Nfert_Hs*Nfert[j]
		a.Hs.subplot.fr[j]<-a.Hs.plot.hat[plot[j]]+b.Nfert_Hs*Nfert[j] # Storing the expected value for j

		# Number of pods damaged by the borer Conopomorpha cramerella

		a.Cc.subplot[j]~dnorm(a.Cc.subplot.hat[j],tau.Cc.subplot)
		a.Cc.subplot.hat[j]<-a.Cc.plot[plot[j]]+b.Nfert_Cc*Nfert[j]
		a.Cc.subplot.fr[j]<-a.Cc.plot.hat[plot[j]]+b.Nfert_Cc*Nfert[j] # Storing the expected value for j
  	}							

	# Plot-level models

	for (k in 1:n.plot){

		# Number of pods harvested   	
		
		a.Npodh.plot[k]~dnorm(a.Npodh.plot.hat[k],tau.Npodh.plot) 
		a.Npodh.plot.hat[k]<-a.Npodh+b.temp_Npodh*temp[k]

		a.Pc.plot[k]~dnorm(a.Pc.plot.hat[k],tau.Pc.plot)			
		a.Pc.plot.hat[k]<-a.Pc+b.temp_Pc*temp[k]+b.age_Pc*age[k]

		# Number of pods damaged by the mirid Helopeltis sulawesi

		a.Hs.plot[k]~dnorm(a.Hs.plot.hat[k],tau.Hs.plot)
		a.Hs.plot.hat[k]<-a.Hs+b.temp_Hs*temp[k]

		# Number of pods damaged by the borer Conopomorpha cramerella

		a.Cc.plot[k]~dnorm(a.Cc.plot.hat[k],tau.Cc.plot)
		a.Cc.plot.hat[k]<-a.Cc+b.temp_Cc*temp[k]
	}



	# Specification of the absolute residuals and simulated predictive values


	for (i in 1:n.tree){

		Npodh.res[i]<-abs(Npodh[i]-Npodh.hat.bound[i])
		Npodh.rep[i]~dpois(Npodh.hat.bound[i])
		Npodh.res.rep[i]<-abs(Npodh.rep[i]-Npodh.hat.bound[i])
		Pc.res[i]<-abs(Pc[i]-p.bound.Pc[i])
		Pc.rep[i]~dbin(p.bound.Pc[i],1)
		Pc.res.rep[i]<-abs(Pc.rep[i]-p.bound.Pc[i])
		Hs.res[i]<-abs(Hs[i]-p.bound.Hs1[i]*new.Npodh[i])
		Hs.rep[i]~dbin(p.bound.Hs1[i],new.Npodh[i])
		Hs.res.rep[i]<-abs(Hs.rep[i]-p.bound.Hs1[i]*Npodh[i])
		Cc.res[i]<-abs(Cc[i]-p.bound.Cc1[i]*new.Npodh[i])
		Cc.rep[i]~dbin(p.bound.Cc1[i],new.Npodh[i])
		Cc.res.rep[i]<-abs(Cc.rep[i]-p.bound.Cc1[i]*Npodh[i])
	}


	# Calculation of the discrepancies observed and simulated

	fit.Npodh<-sum(Npodh.res[])
	fit.Npodh.rep<-sum(Npodh.res.rep[])

	fit.Pc<-sum(Pc.res[])
	fit.Pc.rep<-sum(Pc.res.rep[])

	fit.Hs<-sum(Hs.res[])
	fit.Hs.rep<-sum(Hs.res.rep[])

	fit.Cc<-sum(Cc.res[])
	fit.Cc.rep<-sum(Cc.res.rep[])


	# Specification of the priors
	
	tau.eps.Npodh<-1/sigma2.eps.Npodh
	sigma2.eps.Npodh<-pow(sigma.eps.Npodh,2)
	sigma.eps.Npodh ~ dunif(0, 100)

	b.Pc_Hs~dnorm(0,1.0E-6)

	tau.eps.Hs<-1/sigma2.eps.Hs
	sigma2.eps.Hs<-pow(sigma.eps.Hs,2)
	sigma.eps.Hs ~ dunif(0, 100)

	tau.eps.Cc<-1/sigma2.eps.Cc
	sigma2.eps.Cc<-pow(sigma.eps.Cc,2)
	sigma.eps.Cc ~ dunif(0, 100)


	b.Hs_Cc<-0
	b.Pc_Cc~dnorm(0,1.0E-6)


	b.Nfert_Npodh~dnorm(0,1.0E-6)
	tau.Npodh.subplot<-1/sigma2.Npodh.subplot
	sigma2.Npodh.subplot<-pow(sigma.Npodh.subplot,2)
	sigma.Npodh.subplot ~ dunif(0, 100)

	b.Nfert_Pc~dnorm(0,1.0E-6)
	

	b.Nfert_Hs~dnorm(0,1.0E-6)
	tau.Hs.subplot<-1/sigma2.Hs.subplot
	sigma2.Hs.subplot<-pow(sigma.Hs.subplot,1)
	sigma.Hs.subplot ~ dunif(0, 100) 

	b.Nfert_Cc~dnorm(0,1.0E-6)
	tau.Cc.subplot<-1/sigma2.Cc.subplot
	sigma2.Cc.subplot<-pow(sigma.Cc.subplot,1)
	sigma.Cc.subplot ~ dunif(0, 100) 
	
	
	a.Npodh~dnorm(0,1.0E-6)
	b.temp_Npodh~dnorm(0,1.0E-6)
	tau.Npodh.plot<-1/sigma2.Npodh.plot
	sigma2.Npodh.plot<-pow(sigma.Npodh.plot,2)
	sigma.Npodh.plot ~ dunif(0, 100) 

	a.Pc~dnorm(0,1.0E-6)
	b.temp_Pc~dnorm(0,1.0E-6)
	b.age_Pc~dnorm(0,1.0E-6)
	tau.Pc.plot<-1/sigma2.Pc.plot
	sigma2.Pc.plot<-pow(sigma.Pc.plot,2)
	sigma.Pc.plot ~ dunif(0, 100)
	
	a.Hs~dnorm(0,1.0E-6)
	b.temp_Hs~dnorm(0,1.0E-6)
	tau.Hs.plot<-1/sigma2.Hs.plot
	sigma2.Hs.plot<-pow(sigma.Hs.plot,2)
	sigma.Hs.plot ~ dunif(0, 100)

	a.Cc~dnorm(0,1.0E-6)
	b.temp_Cc~dnorm(0,1.0E-6)
	tau.Cc.plot<-1/sigma2.Cc.plot
	sigma2.Cc.plot<-pow(sigma.Cc.plot,2)
	sigma.Cc.plot ~ dunif(0, 100)


}

# End of the model A